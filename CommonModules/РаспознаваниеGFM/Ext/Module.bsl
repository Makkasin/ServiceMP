
Функция ТранслироватьБуквы(Знач Строка, НаправлениеТрансляцииБукв)
	БуквыРусские	= "аАВсСеЕНКМоОрРТхХу";
	БуквыАнглийские	= "aABcCeEHKMoOpPTxXy";
	КоличествоБукв = СтрДлина(БуквыРусские);
	Для СчетчикБукв = 1 По КоличествоБукв Цикл
		БукваРусская = Сред(БуквыРусские,СчетчикБукв,1);
		БукваАнглийская = Сред(БуквыАнглийские,СчетчикБукв,1);
		Если НаправлениеТрансляцииБукв = "EN-RU" Тогда // Английские в русские
			Строка = СтрЗаменить(Строка,БукваАнглийская,БукваРусская);
		ИначеЕсли НаправлениеТрансляцииБукв = "RU-EN" Тогда // Русские в английские
			Строка = СтрЗаменить(Строка,БукваРусская,БукваАнглийская);
		КонецЕсли;
	КонецЦикла;
	Возврат Строка;
КонецФункции

Функция НайтиТекст(ТабДокумент, СоСтроки = Неопределено, ПоСтроку = Неопределено, СКолонки = Неопределено, ПоКолонку = Неопределено, Знач ТекстДляПоиска, ИгнорироватьРегистр = Истина, НаправлениеТрансляцииБукв = "")
	Если СоСтроки = Неопределено Тогда
		СоСтроки = 1;
	КонецЕсли;
	Если ПоСтроку = Неопределено Тогда
		ПоСтроку = ТабДокумент.ВысотаТаблицы;
	КонецЕсли;
	Если СКолонки = Неопределено Тогда
		СКолонки = 1;
	КонецЕсли;
	Если ПоКолонку = Неопределено Тогда
		ПоКолонку = ТабДокумент.ШиринаТаблицы;
	КонецЕсли;
	МассивТекстов = Новый Массив;
	ТекстДляПоиска = СтрЗаменить(ТекстДляПоиска,"|",Символы.ПС);
	Для СчетчикТекстов = 1 По СтрЧислоСтрок(ТекстДляПоиска) Цикл
		МассивСлов = Новый Массив;
		СтрокаСлов = СтрЗаменить(СтрПолучитьСтроку(ТекстДляПоиска,СчетчикТекстов),"&",Символы.ПС);
		Для СчетчикСлов = 1 По СтрЧислоСтрок(СтрокаСлов) Цикл
			Слово = СтрПолучитьСтроку(СтрокаСлов,СчетчикСлов);
			Если ИгнорироватьРегистр Тогда Слово = НРег(Слово) КонецЕсли;
			Если ЗначениеЗаполнено(НаправлениеТрансляцииБукв) Тогда Слово = ТранслироватьБуквы(Слово,НаправлениеТрансляцииБукв) КонецЕсли;
			МассивСлов.Добавить(Слово);
		КонецЦикла;
		МассивТекстов.Добавить(МассивСлов);
	КонецЦикла;
	Для СчетчикСтрок = СоСтроки По ПоСтроку Цикл
		Для СчетчикКолонок = СКолонки По ПоКолонку Цикл
			Текст = ТабДокумент.Область(СчетчикСтрок, СчетчикКолонок, СчетчикСтрок, СчетчикКолонок).Текст;
			Если ИгнорироватьРегистр Тогда Текст = НРег(Текст) КонецЕсли;
			Если ЗначениеЗаполнено(НаправлениеТрансляцииБукв) Тогда Текст = ТранслироватьБуквы(Текст,НаправлениеТрансляцииБукв) КонецЕсли;
			Для каждого МассивСлов из МассивТекстов Цикл
				КоличествоНайдено = 0;
				Для каждого Слово из МассивСлов Цикл
					Если Найти(Текст,Слово) > 0 Тогда КоличествоНайдено = КоличествоНайдено + 1 КонецЕсли;
				КонецЦикла;
				Если КоличествоНайдено = МассивСлов.Количество() Тогда
					Возврат ТабДокумент.Область(СчетчикСтрок, СчетчикКолонок, СчетчикСтрок, СчетчикКолонок);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

Функция ОпределитьДату(Знач ДатаСтрокой)
	ДатаСтрокой = НРег(ДатаСтрокой);
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," января ",".01.");
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," февраля ",".02.");
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," марта ",".03.");
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," апреля ",".04.");
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," мая ",".05.");
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," июня ",".06.");
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," июля ",".07.");
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," августа ",".08.");
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," сентября ",".09.");
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," октября ",".10.");
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," ноября ",".11.");
	ДатаСтрокой = СтрЗаменить(ДатаСтрокой," декабря ",".12.");
	ДД = "01"; ММ = "01"; ГГГГ = "0001"; Дата = Дата(1,1,1);
	ПозицияТочки = Найти(ДатаСтрокой,".");
	Если ПозицияТочки > 0 Тогда
		ДД = Лев(ДатаСтрокой,ПозицияТочки-1);
		ПодСтрока = Сред(ДатаСтрокой,ПозицияТочки+1);
		ПозицияТочки = Найти(ПодСтрока,".");
		Если ПозицияТочки > 0 Тогда
			ММ = Лев(ПодСтрока,ПозицияТочки-1);
			ПодСтрока = Сред(ПодСтрока,ПозицияТочки+1);
			ПозицияПробела = Найти(ПодСтрока," ");
			Если ПозицияПробела > 0 Тогда
				ГГГГ = Лев(ПодСтрока,ПозицияПробела-1);
			ИначеЕсли НЕ ПустаяСтрока(ПодСтрока) Тогда
				ГГГГ = Лев(ПодСтрока,4);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Попытка
		Дата = Дата(Число(ГГГГ),Число(ММ),Число(ДД));
	Исключение
	КонецПопытки;
	Возврат Дата;
КонецФункции

Функция УдалитьПрефиксы(Знач Строка, Знач Префиксы, ИгнорироватьРегистр = Истина)
	Строка = СокрП(Строка);
	Префиксы = СтрЗаменить(Префиксы,"&",Символы.ПС);
~Повтор:
	Для Счетчик = 1 По СтрЧислоСтрок(Префиксы) Цикл
		Префикс = СтрПолучитьСтроку(Префиксы,Счетчик);
		ДлинаПрефикса = СтрДлина(Префикс);
		Если ДлинаПрефикса > 0 Тогда
			ПодСтрока = Лев(Строка,ДлинаПрефикса);
			Если ПодСтрока = Префикс или ИгнорироватьРегистр и НРег(ПодСтрока) = НРег(Префикс) Тогда
				Строка = Сред(Строка,ДлинаПрефикса+1);
				Перейти ~Повтор;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Строка;
КонецФункции

Функция НайтиНачало(Строка, СимволыДляПоиска, ИгнорироватьРегистр = Истина)
	Если ЗначениеЗаполнено(СимволыДляПоиска) Тогда
		КоличествоСимволов = СтрДлина(Строка);
		Для СчетчикСимволов = 1 По КоличествоСимволов Цикл
			Символ = Сред(Строка,СчетчикСимволов,1);
			Если Найти(СимволыДляПоиска,Символ) > 0 или ИгнорироватьРегистр и Найти(НРег(СимволыДляПоиска),НРег(Символ)) > 0 Тогда
				Возврат СчетчикСимволов;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат 0;
КонецФункции

Функция ПолучитьДанныеСчетФактуры(ТабДокумент)
	ОбластьСФ = НайтиТекст(ТабДокумент,,,,,"Счет&фактура&№|Счет&фак&№|Счет&ура&№|Счет&фактура&N|Счет&фак&N|Счет&ура&N",Истина);
	Если ОбластьСФ <> Неопределено Тогда
		ПозицияНомера = Найти(ОбластьСФ.Текст, "№");
		Если ПозицияНомера = 0 Тогда
			ПозицияНомера = Найти(ОбластьСФ.Текст, "N");
		КонецЕсли;
		ПодСтрока = СокрЛП(Сред(ОбластьСФ.Текст,ПозицияНомера+1));
		ПозицияОт = Найти(ПодСтрока, "от");
		Если ПозицияОт > 0 Тогда
			Номер = СокрЛП(Сред(ПодСтрока,1,ПозицияОт-1));
			Дата = ОпределитьДату(СокрЛП(Сред(ПодСтрока,ПозицияОт+2)));
		Иначе
			Номер = ПодСтрока;
			Дата = Дата(1,1,1);
			ОбластьДата = НайтиТекст(ТабДокумент,ОбластьСФ.Верх,,,,"января|от&янв|февраля|от&фев|марта|от&мар|апреля|от&апр|мая|от&мая|июня|от&июн|июля|от&июл|августа|от&авг|сентября|от&сен|октября|от&окт|ноября|от&ноя|декабря|от&дек",Истина);
			Если ОбластьДата <> Неопределено Тогда
				ПозицияЦифр = НайтиНачало(ОбластьДата.Текст, "0123456789", Ложь);
				Дата = ОпределитьДату(СокрП(Сред(ОбластьДата.Текст,ПозицияЦифр)));
			КонецЕсли;
		КонецЕсли;
		ДанныеСчетФактуры = Новый Структура("Номер,Дата,ПродавецНаименование,ПродавецИНН,ПродавецКПП,ПокупательНаименование,ПокупательИНН,ПокупательКПП,Основание,Строки",Номер,Дата,,,,,,,,Новый Массив);
		// ...
		ОбластьПродавец = НайтиТекст(ТабДокумент,ОбластьСФ.Низ+1,,,,"Продавец",Истина);
		ОбластьПокупатель = НайтиТекст(ТабДокумент,ОбластьСФ.Низ+1,,,,"Покупатель",Истина);
		ОбластьНаименованиеТовара = НайтиТекст(ТабДокумент,ОбластьСФ.Низ+1,,,,"Наименование&товара|менование&товара&услуг|Наименование&услуг|Наимен&услуг|Наименование&имуществ&права",Истина,"EN-RU");
		// Продавец
		ОбластьИННКПП1 = Неопределено;
		Если ОбластьПродавец <> Неопределено Тогда
			ДанныеСчетФактуры.ПродавецНаименование = УдалитьПрефиксы(ОбластьПродавец.Текст,"Продавец&:& ");
			Если ОбластьПокупатель <> Неопределено Тогда
				ОбластьИННКПП1 = НайтиТекст(ТабДокумент,ОбластьПродавец.Низ+1,ОбластьПокупатель.Верх-1,,,"ИНН&КПП|КПП|ИНН",Истина);
			КонецЕсли;
		КонецЕсли;
		Если ОбластьИННКПП1 = Неопределено Тогда
			ОбластьИННКПП1 = НайтиТекст(ТабДокумент,ОбластьСФ.Низ+1,,,,"ИНН&КПП|КПП|ИНН",Истина); // 1-ый ИНН Продавца
		КонецЕсли;
		Если ОбластьИННКПП1 <> Неопределено Тогда
			ПозицияЦифр = НайтиНачало(ОбластьИННКПП1.Текст, "0123456789", Ложь);
			Если ПозицияЦифр > 0 Тогда
				ИННКПП = СокрП(Сред(ОбластьИННКПП1.Текст,ПозицияЦифр));
			Иначе
				ИННКПП = УдалитьПрефиксы(ОбластьИННКПП1.Текст,"ИНН&/&\&КПП&продавца&:& ");
			КонецЕсли;
			ПозицияСлэша = Найти(ИННКПП,"\");
			Если ПозицияСлэша = 0 Тогда
				ПозицияСлэша = Найти(ИННКПП,"/");
			КонецЕсли;
			Если ПозицияСлэша > 0 Тогда
				ДанныеСчетФактуры.ПродавецИНН = СокрЛП(Лев(ИННКПП,ПозицияСлэша-1));
				ДанныеСчетФактуры.ПродавецКПП = СокрЛП(Сред(ИННКПП,ПозицияСлэша+1));
			Иначе
				ДанныеСчетФактуры.ПродавецИНН = СокрЛП(ИННКПП);
				ДанныеСчетФактуры.ПродавецКПП = "";
			КонецЕсли;
		КонецЕсли;
		// Покупатель
		ОбластьИННКПП2 = Неопределено;
		Если ОбластьПокупатель <> Неопределено Тогда
			ДанныеСчетФактуры.ПокупательНаименование = УдалитьПрефиксы(ОбластьПокупатель.Текст,"Покупатель&:& ");
			Если ОбластьНаименованиеТовара <> Неопределено Тогда
				ОбластьИННКПП2 = НайтиТекст(ТабДокумент,ОбластьПокупатель.Низ+1,ОбластьНаименованиеТовара.Верх-1,,,"ИНН&КПП|КПП|ИНН",Истина);
			КонецЕсли;
		КонецЕсли;
		Если ОбластьИННКПП2 = Неопределено Тогда
			Если ОбластьИННКПП1 <> Неопределено Тогда
				ОбластьИННКПП2 = НайтиТекст(ТабДокумент,ОбластьИННКПП1.Низ+1,,,,"ИНН&КПП|КПП|ИНН",Истина); // 2-ой ИНН Покупателя
			КонецЕсли;
		КонецЕсли;
		Если ОбластьИННКПП2 <> Неопределено Тогда
			ПозицияЦифр = НайтиНачало(ОбластьИННКПП2.Текст, "0123456789", Ложь);
			Если ПозицияЦифр > 0 Тогда
				ИННКПП = СокрП(Сред(ОбластьИННКПП2.Текст,ПозицияЦифр));
			Иначе
				ИННКПП = УдалитьПрефиксы(ОбластьИННКПП2.Текст,"ИНН&/&\&КПП&покупателя&:& ");
			КонецЕсли;
			ПозицияСлэша = Найти(ИННКПП,"\");
			Если ПозицияСлэша = 0 Тогда
				ПозицияСлэша = Найти(ИННКПП,"/");
			КонецЕсли;
			Если ПозицияСлэша > 0 Тогда
				ДанныеСчетФактуры.ПокупательИНН = СокрЛП(Лев(ИННКПП,ПозицияСлэша-1));
				ДанныеСчетФактуры.ПокупательКПП = СокрЛП(Сред(ИННКПП,ПозицияСлэша+1));
			Иначе
				ДанныеСчетФактуры.ПокупательИНН = СокрЛП(ИННКПП);
				ДанныеСчетФактуры.ПокупательКПП = "";
			КонецЕсли;
		КонецЕсли;
		// Товары
		Если ОбластьНаименованиеТовара <> Неопределено Тогда
			// Основание
			Если ОбластьПокупатель <> Неопределено Тогда
				ОбластьОснование = НайтиТекст(ТабДокумент,ОбластьПокупатель.Низ+1,ОбластьНаименованиеТовара.Верх-1,,,"Основание",Истина);
			Иначе
				ОбластьОснование = НайтиТекст(ТабДокумент,ОбластьСФ.Низ+1,ОбластьНаименованиеТовара.Верх-1,,,"Основание",Истина);
			КонецЕсли;
			Если ОбластьОснование <> Неопределено Тогда
				ДанныеСчетФактуры.Основание = УдалитьПрефиксы(ОбластьОснование.Текст,"Основание&:& ");
			КонецЕсли;
			// ...
			СПозиции = ОбластьНаименованиеТовара.Низ+1;
			Если СокрЛП(ТабДокумент.Область(СПозиции, ОбластьНаименованиеТовара.Лево, СПозиции, ОбластьНаименованиеТовара.Лево).Текст) = "1" или
					СокрЛП(ТабДокумент.Область(СПозиции, ОбластьНаименованиеТовара.Лево+1, СПозиции, ОбластьНаименованиеТовара.Лево+1).Текст) = "1а" Тогда
				СПозиции = СПозиции + 1;
			ИначеЕсли СокрЛП(ТабДокумент.Область(СПозиции+1, ОбластьНаименованиеТовара.Лево, СПозиции+1, ОбластьНаименованиеТовара.Лево).Текст) = "1" или
						СокрЛП(ТабДокумент.Область(СПозиции+1, ОбластьНаименованиеТовара.Лево+1, СПозиции+1, ОбластьНаименованиеТовара.Лево+1).Текст) = "1а" Тогда
				СПозиции = СПозиции + 2;
			Иначе
				Для СчетчикСтрок = СПозиции По СПозиции+5 Цикл
					СчетчикСовпадений = 0;
					Для СчетчикКолонок = ОбластьНаименованиеТовара.Лево По ОбластьНаименованиеТовара.Лево+15 Цикл
						Если Найти(",1,1а,2,2а,3,4,5,6,7,9,10,10а,",","+СокрЛП(ТабДокумент.Область(СчетчикСтрок, СчетчикКолонок, СчетчикСтрок, СчетчикКолонок).Текст)+",") > 0 Тогда
							СчетчикСовпадений = СчетчикСовпадений + 1;
						КонецЕсли;
					КонецЦикла;
					Если СчетчикСовпадений >= 7 Тогда
						СПозиции = СчетчикСтрок + 1; Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ПоПозицию = Неопределено;
			ОбластьВсегоКОплате = НайтиТекст(ТабДокумент,СПозиции,,,,"Всего к оплате",Истина,"EN-RU");
			Если ОбластьВсегоКОплате = Неопределено Тогда
				Для СчетчикСтрок = СПозиции По ТабДокумент.ВысотаТаблицы Цикл
					Ячейка = ТабДокумент.Область(СчетчикСтрок,ОбластьНаименованиеТовара.Лево,СчетчикСтрок,ОбластьНаименованиеТовара.Лево);
					Если Ячейка.ГраницаСверху.Толщина <> 0 или Ячейка.ГраницаСнизу.Толщина <> 0 или Ячейка.ГраницаСлева.Толщина <> 0 или Ячейка.ГраницаСправа.Толщина <> 0 Тогда
						ОбластьВсегоКОплате = Ячейка;
					Иначе Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ПоПозицию = ?(ОбластьВсегоКОплате <> Неопределено, ОбластьВсегоКОплате.Верх-1, ТабДокумент.ВысотаТаблицы);
			СтрокаКолонок = "ТовараНаименование,ТоварКод,ЕИНаименование,ЕИКод,Количество,Цена,Стоимость,СуммаАкциза,СтавкаНДС,СуммаНДС,Всего,СтранаКод,СтранаНаименование,ГТД";
			// Определим колонки табличного документа
			Колонки = Новый Структура(СтрокаКолонок);
			Колонки.ТовараНаименование = ОбластьНаименованиеТовара.Лево;
			Для СчетчикКолонок = ОбластьНаименованиеТовара.Лево + 1 По ТабДокумент.ШиринаТаблицы Цикл
				Строка = НРег(ТабДокумент.Область(ОбластьНаименованиеТовара.Низ, СчетчикКолонок, ОбластьНаименованиеТовара.Низ, СчетчикКолонок).Текст);
				ПодСтрока = НРег(ТабДокумент.Область(ОбластьНаименованиеТовара.Низ+1, СчетчикКолонок, ОбластьНаименованиеТовара.Низ+1, СчетчикКолонок).Текст);
				СовокупнаяСтрока = Строка + Символы.ПС + ПодСтрока;
				Если Найти(СовокупнаяСтрока,"код") > 0 и Найти(СовокупнаяСтрока,"вид") > 0 и Найти(СовокупнаяСтрока,"товар") > 0 Тогда
					Колонки.ТоварКод = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"единица") > 0 и Найти(СовокупнаяСтрока,"измерения") > 0 и Найти(СовокупнаяСтрока,"код") > 0 Тогда
					Колонки.ЕИКод = СчетчикКолонок;
					Колонки.ЕИНаименование = СчетчикКолонок + 1;
				ИначеЕсли Найти(СовокупнаяСтрока,"количество") > 0 или Найти(СовокупнаяСтрока,"коли") > 0 и Найти(СовокупнаяСтрока,"чество") > 0 Тогда
					Колонки.Количество = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"цена") > 0 или Найти(СовокупнаяСтрока,"тариф") > 0 Тогда
					Колонки.Цена = СчетчикКолонок;
				ИначеЕсли (Найти(СовокупнаяСтрока,"товар") > 0 или Найти(СовокупнаяСтрока,"работ") > 0 или Найти(СовокупнаяСтрока,"услуг") > 0) и Найти(СовокупнаяСтрока,"без налога") > 0 Тогда
					Колонки.Стоимость = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"сумма") > 0 и Найти(СовокупнаяСтрока,"акциз") > 0 Тогда
					Колонки.СуммаАкциза = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"налоговая") > 0 и Найти(СовокупнаяСтрока,"ставка") > 0 или Найти(СовокупнаяСтрока,"логовая ста") > 0 Тогда
					Колонки.СтавкаНДС = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"сумма") > 0 и Найти(СовокупнаяСтрока,"налога") > 0 Тогда
					Колонки.СуммаНДС = СчетчикКолонок;
				ИначеЕсли (Найти(СовокупнаяСтрока,"товар") > 0 или Найти(СовокупнаяСтрока,"работ") > 0 или Найти(СовокупнаяСтрока,"услуг") > 0) и Найти(СовокупнаяСтрока,"с налогом") > 0 Тогда
					Колонки.Всего = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"страна") > 0 и Найти(СовокупнаяСтрока,"происхождения") > 0 и Найти(СовокупнаяСтрока,"код") > 0 Тогда
					Колонки.СтранаКод = СчетчикКолонок;
					Колонки.СтранаНаименование = СчетчикКолонок+1;
					Колонки.ГТД = СчетчикКолонок+2;
				КонецЕсли;
			КонецЦикла;
			// Чтение табличной части
			Для СчетчикСтрок = СПозиции По ПоПозицию Цикл
				СтрокаТабличнойЧасти = Новый Структура(СтрокаКолонок);
				Для каждого Колонка из Колонки Цикл
					Если Колонка.Значение <> Неопределено Тогда
						СтрокаТабличнойЧасти[Колонка.Ключ] = ТабДокумент.Область(СчетчикСтрок,Колонка.Значение,СчетчикСтрок,Колонка.Значение).Текст;
					КонецЕсли;
				КонецЦикла;
				ДанныеСчетФактуры.Строки.Добавить(СтрокаТабличнойЧасти);
			КонецЦикла;
			// ...
		КонецЕсли;
		Возврат ДанныеСчетФактуры;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ПолучитьДанныеПриходногоОрдера(ТабДокумент)
	ОбластьПО = НайтиТекст(ТабДокумент,,,,,"Приход&ордер&№|Приход&ордер&N",Истина);
	Если ОбластьПО <> Неопределено Тогда
		ПозицияНомера = Найти(ОбластьПО.Текст, "№");
		Если ПозицияНомера = 0 Тогда
			ПозицияНомера = Найти(ОбластьПО.Текст, "N");
		КонецЕсли;
		Номер = СокрЛП(Сред(ОбластьПО.Текст,ПозицияНомера+1));
		Дата = Дата(1,1,1);
		ДанныеПриходногоОрдера = Новый Структура("Номер,Дата,ПоставщикНаименование,ПоставщикКод,СкладНаименование,НомерСопроводительногоДокумента,Строки",Номер,Дата,,,,,Новый Массив);
		// ...
		ОбластьДатаСоставления = НайтиТекст(ТабДокумент,ОбластьПО.Низ+1,,,,"Дата&состав&ления",Истина);
		ОбластьПоставщик = НайтиТекст(ТабДокумент,ОбластьПО.Низ+1,,,,"Поставщик",Истина);
		ОбластьСклад = НайтиТекст(ТабДокумент,ОбластьПО.Низ+1,,,,"Склад",Истина);
		ОбластьНомерСопроводительногоДокумента = НайтиТекст(ТабДокумент,ОбластьПО.Низ+1,,,,"Номер&документа",Истина);
		ОбластьМатериальныеЦенности = НайтиТекст(ТабДокумент,ОбластьПО.Низ+1,,,,"Наименование&сорт&размер&марка",Истина);
		// Дата составления
		Если ОбластьДатаСоставления <> Неопределено Тогда
			ДанныеПриходногоОрдера.Дата = ОпределитьДату(ТабДокумент.Область(ОбластьДатаСоставления.Верх+2,ОбластьДатаСоставления.Лево,ОбластьДатаСоставления.Верх+2,ОбластьДатаСоставления.Лево).Текст);
		КонецЕсли;
		// Поставщик
		Если ОбластьПоставщик <> Неопределено Тогда
			ДанныеПриходногоОрдера.ПоставщикНаименование = ТабДокумент.Область(ОбластьПоставщик.Верх+2,ОбластьПоставщик.Лево,ОбластьПоставщик.Верх+2,ОбластьПоставщик.Лево).Текст;
			ДанныеПриходногоОрдера.ПоставщикКод = ТабДокумент.Область(ОбластьПоставщик.Верх+2,ОбластьПоставщик.Лево+1,ОбластьПоставщик.Верх+2,ОбластьПоставщик.Лево+1).Текст;
		КонецЕсли;
		// Склад
		Если ОбластьСклад <> Неопределено Тогда
			ДанныеПриходногоОрдера.СкладНаименование = УдалитьПрефиксы(ТабДокумент.Область(ОбластьСклад.Верх+2,ОбластьСклад.Лево,ОбластьСклад.Верх+2,ОбластьСклад.Лево).Текст,"Склад&:& ");
		КонецЕсли;
		// Номер сопроводительного документа (№ с/ф)
		Если ОбластьНомерСопроводительногоДокумента <> Неопределено Тогда
			ДанныеПриходногоОрдера.НомерСопроводительногоДокумента = ТабДокумент.Область(ОбластьНомерСопроводительногоДокумента.Верх+2,ОбластьНомерСопроводительногоДокумента.Лево,ОбластьНомерСопроводительногоДокумента.Верх+2,ОбластьНомерСопроводительногоДокумента.Лево).Текст;
		КонецЕсли;
		// Материальные ценности
		Если ОбластьМатериальныеЦенности <> Неопределено Тогда
			СПозиции = ОбластьМатериальныеЦенности.Низ+1;
			Если СокрЛП(ТабДокумент.Область(СПозиции, ОбластьМатериальныеЦенности.Лево, СПозиции, ОбластьМатериальныеЦенности.Лево).Текст) = "1" Тогда
				СПозиции = СПозиции + 1;
			Иначе
				Для СчетчикСтрок = СПозиции По СПозиции+5 Цикл
					СчетчикСовпадений = 0;
					Для СчетчикКолонок = ОбластьМатериальныеЦенности.Лево По ОбластьМатериальныеЦенности.Лево+15 Цикл
						Если Найти(",1,2,3,4,5,6,7,9,10,11,12,",","+СокрЛП(ТабДокумент.Область(СчетчикСтрок, СчетчикКолонок, СчетчикСтрок, СчетчикКолонок).Текст)+",") > 0 Тогда
							СчетчикСовпадений = СчетчикСовпадений + 1;
						КонецЕсли;
					КонецЦикла;
					Если СчетчикСовпадений >= 7 Тогда
						СПозиции = СчетчикСтрок + 1; Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ПоПозицию = Неопределено;
			ОбластьИтого = НайтиТекст(ТабДокумент,СПозиции,,,,"Итого",Истина);
			Если ОбластьИтого = Неопределено Тогда
				Для СчетчикСтрок = СПозиции По ТабДокумент.ВысотаТаблицы Цикл
					Ячейка = ТабДокумент.Область(СчетчикСтрок,ОбластьМатериальныеЦенности.Лево,СчетчикСтрок,ОбластьМатериальныеЦенности.Лево);
					Если Ячейка.ГраницаСверху.Толщина <> 0 или Ячейка.ГраницаСнизу.Толщина <> 0 или Ячейка.ГраницаСлева.Толщина <> 0 или Ячейка.ГраницаСправа.Толщина <> 0 Тогда
						ОбластьИтого = Ячейка;
					Иначе Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ПоПозицию = ?(ОбластьИтого <> Неопределено, ОбластьИтого.Верх-1, ТабДокумент.ВысотаТаблицы);
			СтрокаКолонок = "ТовараНаименование,ТоварКод,ЕИНаименование,ЕИКод,Количество,Цена,СуммаБезНДС,СуммаНДС,Всего";
			// Определим колонки табличного документа
			Колонки = Новый Структура(СтрокаКолонок);
			Колонки.ТовараНаименование = ОбластьМатериальныеЦенности.Лево;
			Для СчетчикКолонок = ОбластьМатериальныеЦенности.Лево + 1 По ТабДокумент.ШиринаТаблицы Цикл
				Строка = НРег(ТабДокумент.Область(ОбластьМатериальныеЦенности.Верх, СчетчикКолонок, ОбластьМатериальныеЦенности.Верх, СчетчикКолонок).Текст);
				ПредСтрока = НРег(ТабДокумент.Область(ОбластьМатериальныеЦенности.Верх-1, СчетчикКолонок, ОбластьМатериальныеЦенности.Верх-1, СчетчикКолонок).Текст);
				СовокупнаяСтрока = ПредСтрока + Символы.ПС + Строка;
				Если Найти(СовокупнаяСтрока,"номенкла") > 0 и Найти(СовокупнаяСтрока,"турный") > 0 и Найти(СовокупнаяСтрока,"номер") > 0 Тогда
					Колонки.ТоварКод = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"единица") > 0 и Найти(СовокупнаяСтрока,"измерения") > 0 и Найти(СовокупнаяСтрока,"код") > 0 Тогда
					Колонки.ЕИКод = СчетчикКолонок;
					Колонки.ЕИНаименование = СчетчикКолонок + 1;
				ИначеЕсли Найти(СовокупнаяСтрока,"принято") > 0 Тогда
					Колонки.Количество = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"цена") > 0 Тогда
					Колонки.Цена = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"сумма") > 0 и Найти(СовокупнаяСтрока,"без") > 0 и Найти(СовокупнаяСтрока,"учета") > 0 и Найти(СовокупнаяСтрока,"ндс") > 0 Тогда
					Колонки.СуммаБезНДС = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"сумма") > 0 и Найти(СовокупнаяСтрока,"ндс") > 0 Тогда
					Колонки.СуммаНДС = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"всего") > 0 и Найти(СовокупнаяСтрока,"с учетом") > 0 и Найти(СовокупнаяСтрока,"ндс") > 0 Тогда
					Колонки.Всего = СчетчикКолонок;
				КонецЕсли;
			КонецЦикла;
			// Чтение табличной части
			Для СчетчикСтрок = СПозиции По ПоПозицию Цикл
				СтрокаТабличнойЧасти = Новый Структура(СтрокаКолонок);
				Для каждого Колонка из Колонки Цикл
					Если Колонка.Значение <> Неопределено Тогда
						СтрокаТабличнойЧасти[Колонка.Ключ] = ТабДокумент.Область(СчетчикСтрок,Колонка.Значение,СчетчикСтрок,Колонка.Значение).Текст;
					КонецЕсли;
				КонецЦикла;
				ДанныеПриходногоОрдера.Строки.Добавить(СтрокаТабличнойЧасти);
			КонецЦикла;
			// ...
		КонецЕсли;
		Возврат ДанныеПриходногоОрдера;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ПолучитьДанныеЛЗК(ТабДокумент)
	ОбластьЛЗК = НайтиТекст(ТабДокумент,,,,,"Лимит&забор&карта&№|Лимит&забор&карта&N",Истина);
	Если ОбластьЛЗК <> Неопределено Тогда
		ПозицияНомера = Найти(ОбластьЛЗК.Текст, "№");
		Если ПозицияНомера = 0 Тогда
			ПозицияНомера = Найти(ОбластьЛЗК.Текст, "N");
		КонецЕсли;
		ПодСтрока = СокрЛП(Сред(ОбластьЛЗК.Текст,ПозицияНомера+1));
		ПозицияОт = Найти(ПодСтрока, "от");
		Если ПозицияОт > 0 Тогда
			Номер = СокрЛП(Сред(ПодСтрока,1,ПозицияОт-1));
			Дата = ОпределитьДату(СокрЛП(Сред(ПодСтрока,ПозицияОт+2)));
		Иначе
			Номер = ПодСтрока;
			Дата = Дата(1,1,1);
		КонецЕсли;
		ДанныеЛЗК = Новый Структура("Номер,Дата,СкладНаименование,АвтомобильНаименование,Строки",Номер,Дата,,,Новый Массив);
		// ...
		ОбластьСклад = НайтиТекст(ТабДокумент,ОбластьЛЗК.Низ+1,,,,"Склад",Истина);
		ОбластьАвтомобиль = НайтиТекст(ТабДокумент,ОбластьЛЗК.Низ+1,,,,"Автомобиль",Истина);
		ОбластьМатериальныеЦенности = НайтиТекст(ТабДокумент,ОбластьЛЗК.Низ+1,,,,"Наименование&запчаст&материал",Истина);
		// Склад
		Если ОбластьСклад <> Неопределено Тогда
			ДанныеЛЗК.СкладНаименование = УдалитьПрефиксы(ТабДокумент.Область(ОбластьСклад.Верх,ОбластьСклад.Лево,ОбластьСклад.Верх,ОбластьСклад.Лево).Текст,"Склад&:& ");
		КонецЕсли;
		// Автомобиль
		Если ОбластьАвтомобиль <> Неопределено Тогда
			ДанныеЛЗК.АвтомобильНаименование = УдалитьПрефиксы(ТабДокумент.Область(ОбластьАвтомобиль.Верх,ОбластьАвтомобиль.Лево,ОбластьАвтомобиль.Верх,ОбластьАвтомобиль.Лево).Текст,"Автомобиль&:& ");
		КонецЕсли;
		// Материальные ценности
		Если ОбластьМатериальныеЦенности <> Неопределено Тогда
			СПозиции = ОбластьМатериальныеЦенности.Верх + ?(ПустаяСтрока(ТабДокумент.Область(ОбластьМатериальныеЦенности.Верх+1,ОбластьМатериальныеЦенности.Лево,ОбластьМатериальныеЦенности.Верх+1,ОбластьМатериальныеЦенности.Лево).Текст), 2, 1);
			ПоПозицию = Неопределено;
			ОбластьИтого = НайтиТекст(ТабДокумент,СПозиции,,,,"Итого",Истина);
			Если ОбластьИтого = Неопределено Тогда
				Для СчетчикСтрок = СПозиции По ТабДокумент.ВысотаТаблицы Цикл
					Ячейка = ТабДокумент.Область(СчетчикСтрок,ОбластьМатериальныеЦенности.Лево,СчетчикСтрок,ОбластьМатериальныеЦенности.Лево);
					Если Ячейка.ГраницаСверху.Толщина <> 0 или Ячейка.ГраницаСнизу.Толщина <> 0 или Ячейка.ГраницаСлева.Толщина <> 0 или Ячейка.ГраницаСправа.Толщина <> 0 Тогда
						ОбластьИтого = Ячейка;
					Иначе Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ПоПозицию = ?(ОбластьИтого <> Неопределено, ОбластьИтого.Верх-1, ТабДокумент.ВысотаТаблицы);
			СтрокаКолонок = "ТовараНаименование,ТоварКод,ЕИНаименование,Количество,Цена,Сумма";
			// Определим колонки табличного документа
			Колонки = Новый Структура(СтрокаКолонок);
			Колонки.ТовараНаименование = ОбластьМатериальныеЦенности.Лево;
			Для СчетчикКолонок = 1 По ТабДокумент.ШиринаТаблицы Цикл
				Если СчетчикКолонок = Колонки.ТовараНаименование Тогда
					Продолжить;
				КонецЕсли;
				Строка = НРег(ТабДокумент.Область(ОбластьМатериальныеЦенности.Верх, СчетчикКолонок, ОбластьМатериальныеЦенности.Верх, СчетчикКолонок).Текст);
				ПодСтрока = ?(СПозиции - ОбластьМатериальныеЦенности.Верх > 1, НРег(ТабДокумент.Область(ОбластьМатериальныеЦенности.Верх+1, СчетчикКолонок, ОбластьМатериальныеЦенности.Верх+1, СчетчикКолонок).Текст), "");
				СовокупнаяСтрока = Строка + Символы.ПС + ПодСтрока;
				Если Найти(СовокупнаяСтрока,"номенкл") > 0 и Найти(СовокупнаяСтрока,"№") > 0 Тогда
					Колонки.ТоварКод = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"ед") > 0 и Найти(СовокупнаяСтрока,"изм") > 0 Тогда
					Колонки.ЕИНаименование = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"кол-во") > 0 Тогда
					Колонки.Количество = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"цена") > 0 или Найти(СовокупнаяСтрока,"цен") > 0 и Найти(СовокупнаяСтрока,"за") > 0 или Найти(СовокупнаяСтрока,"це") > 0 и Найти(СовокупнаяСтрока,"за") > 0 и Найти(СовокупнаяСтрока,"ед") > 0 Тогда
					Колонки.Цена = СчетчикКолонок;
				ИначеЕсли Найти(СовокупнаяСтрока,"сумма") > 0 Тогда
					Колонки.Сумма = СчетчикКолонок;
				КонецЕсли;
			КонецЦикла;
			// Чтение табличной части
			Если Колонки.Количество <> Неопределено или Колонки.Цена <> Неопределено или Колонки.Сумма <> Неопределено Тогда
				Для СчетчикСтрок = СПозиции По ПоПозицию Цикл
					СтрокаТабличнойЧасти = Новый Структура(СтрокаКолонок);
					Для каждого Колонка из Колонки Цикл
						Если Колонка.Значение <> Неопределено Тогда
							СтрокаТабличнойЧасти[Колонка.Ключ] = ТабДокумент.Область(СчетчикСтрок,Колонка.Значение,СчетчикСтрок,Колонка.Значение).Текст;
						КонецЕсли;
					КонецЦикла;
					ДанныеЛЗК.Строки.Добавить(СтрокаТабличнойЧасти);
				КонецЦикла;
			КонецЕсли;
			// ...
		КонецЕсли;
		Возврат ДанныеЛЗК;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ПрочитатьНаСервере(ВременныйФайлXLS) Экспорт
//Функция ПрочитатьНаСервере(Адрес) Экспорт
//	ВременныйФайлXLS = ПолучитьИмяВременногоФайла("xlsx");
//	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
//	ДвоичныеДанные.Записать(ВременныйФайлXLS);
//	УдалитьИзВременногоХранилища(Адрес);
//	ДвоичныеДанные = Неопределено;
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.Прочитать(ВременныйФайлXLS, СпособЧтенияЗначенийТабличногоДокумента.Текст, ТипФайлаТабличногоДокумента.XLSX);
	УдалитьФайлы(ВременныйФайлXLS);
	Возврат Новый Структура("ТабличныйДокумент, ДанныеСчетФактуры, ДанныеПриходногоОрдера, ДанныеЛЗК", ТабДокумент, ПолучитьДанныеСчетФактуры(ТабДокумент), ПолучитьДанныеПриходногоОрдера(ТабДокумент), ПолучитьДанныеЛЗК(ТабДокумент));
КонецФункции


Процедура Прочитать(Команда)
	//Адрес = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	//ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ФайлXLS), Адрес);
	//Данные = ПрочитатьНаСервере(Адрес);
	//// Прочитанный средствами 1С Excel файл
	//Данные.ТабличныйДокумент.Показать("Excel файл");
	//// Дешифрованные данные
	//ПоказатьДанные(Данные.ДанныеСчетФактуры,"Счет-фактура");
	//ПоказатьДанные(Данные.ДанныеПриходногоОрдера,"Приходный ордер");
КонецПроцедуры

Процедура ПоказатьДанные(Данные,Заголовок)
	Если Данные <> Неопределено Тогда
		ТабДокумент = Новый ТабличныйДокумент;
		СчетчикСтрок = 1; // Отступ
		Для каждого ЭлементСтруктуры из Данные Цикл
			Если ЭлементСтруктуры.Ключ = "Строки" Тогда Продолжить КонецЕсли;
			СчетчикСтрок = СчетчикСтрок + 1;
			ТабДокумент.Область(СчетчикСтрок,1,СчетчикСтрок,1).Текст = ЭлементСтруктуры.Ключ;
			ТабДокумент.Область(СчетчикСтрок,2,СчетчикСтрок,2).Текст = ЭлементСтруктуры.Значение;
		КонецЦикла;
		Колонки = Неопределено;
		СчетчикСтрок = СчетчикСтрок + 1; // Разрыв
		Для каждого СтрокаТабличнойЧасти из Данные.Строки Цикл
			Если Колонки = Неопределено Тогда
				Колонки = Новый Массив;
				СчетчикСтрок = СчетчикСтрок + 1;
				Для каждого ЭлементСтруктуры из СтрокаТабличнойЧасти Цикл
					Колонки.Добавить(ЭлементСтруктуры.Ключ);
					СччетчикКолонок = Колонки.Количество();
					ТабДокумент.Область(СчетчикСтрок,СччетчикКолонок,СчетчикСтрок,СччетчикКолонок).Текст = ЭлементСтруктуры.Ключ;
				КонецЦикла;
			КонецЕсли;
			СчетчикСтрок = СчетчикСтрок + 1;
			Для СччетчикКолонок = 1 По Колонки.Количество() Цикл
				Колонка = Колонки.Получить(СччетчикКолонок-1);
				ТабДокумент.Область(СчетчикСтрок,СччетчикКолонок,СчетчикСтрок,СччетчикКолонок).Текст = СтрокаТабличнойЧасти[Колонка];
			КонецЦикла;
		КонецЦикла;
		ТабДокумент.Показать(Заголовок);
	КонецЕсли;
КонецПроцедуры
